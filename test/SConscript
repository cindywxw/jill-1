import os
Import('env lib')

# boost libraries may be named differently
BOOST_LIBS = ['boost_system','boost_date_time','boost_program_options','boost_filesystem']
if hasattr(os,'uname') and os.uname()[0] == 'Darwin':
    BOOST_LIBS = [x + "-mt" for x in BOOST_LIBS]

# clone environment and add libraries for modules
menv = env.Clone()
menv.Append(CPPPATH=['#'],
            LIBS=['jack','samplerate','hdf5','hdf5_hl','sndfile','zmq'] + BOOST_LIBS,
            )

out = [menv.Program(os.path.splitext(str(f))[0],[f,lib]) for f in env.Glob("*.cc")] + \
    [menv.Program(os.path.splitext(str(f))[0],[f]) for f in env.Glob("*.c")]

env.Alias('test',out)


