import os
Import('env lib')

# clone environment and add libraries for modules
menv = env.Clone()
menv.Append(CPPPATH=['#'],
            LIBS=['jack','samplerate','hdf5','hdf5_hl','sndfile',
                  'boost_date_time','boost_program_options','boost_filesystem'])

programs = {'jill_mixer' : ['jill_mixer.cc'],
            'jill_delay' : ['jill_delay.cc'],
            'jill_writer' : ['jill_writer.cc'],
            'jill_play' : ['jill_play.cc'],
            'jill_thresh' : ['jill_thresh.cc'],
            'jill_capture' : ['triggered_writer.cc', 'jill_capture.cc']
            }

out = []

for progname,srcs in programs.items():
    prog = menv.Program(progname,srcs+[lib])
    menv.Alias(progname,prog)
    out.append(prog)
    env.Alias('install', env.Install(os.path.join(env['PREFIX'],'bin'),prog))

env.Alias('modules',out)
    
# scripts
scripts = ['jill_trigger_capture']
env.Alias('install', env.Install(os.path.join(env['PREFIX'],'bin'),scripts))

